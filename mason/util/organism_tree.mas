<%doc>
=head1 NAME

organism_tree.mas - mason component for printing organism phylo trees

=head1 DESCRIPTION

Queries the database for web_visible (see chado: organismprop table )
sgn organisms, and buids a species tree from the phylonode table using
the L<CXGN::Phylo::Tree> framework

=head1 AUTHOR

Naama Menda <nm249@cornell.edu>

=cut


</%doc>

<%args>
    $schema
    $root
    $species_hashref
    $speciesinfo_cache => undef
    $uri_dir
    $tmp_dir
    $image_only => 0
</%args>

<%perl>
use Cache::File;
use Path::Class;

use CXGN::Phylo::OrganismTree;
use CXGN::Tools::WebImageCache;

my $cache = CXGN::Tools::WebImageCache->new();
$cache->set_temp_dir( $uri_dir );
$cache->set_basedir( $tmp_dir );

my $file_key_name = join '-', $root, sort keys %$species_hashref;
$cache->set_key( $file_key_name );
$cache->set_expiration_time( 86400 );

my $map_name= $root . "_map";

$cache->set_map_name( $map_name );

$uri_dir = dir( $tmp_dir, $uri_dir );
$uri_dir->mkpath;
my $newick_file = $uri_dir->file( "$root.newick" );

unless( $cache->is_valid ) {

    my $tree = CXGN::Phylo::OrganismTree->new( $schema );
    my $newick_string = $tree->build_tree( $root, $species_hashref, $speciesinfo_cache );

    $newick_file->openw->print( $newick_string );

    my $image_map = $tree->get_renderer()
                         ->get_html_image_map( $map_name );
    my $image_png = $tree->render_png( undef, 1 );

    $cache->set_image_data($image_png);
    $cache->set_image_map_data($image_map);

}

my $image_html  = $cache->get_image_html();
my $tree_string = $newick_file->slurp;

</%perl>

% if( $image_only ) {

  <% $image_html %>

% } else {

  <a name="<% $root |h %>"></a>

  <&| /page/info_section.mas,
    title         => $root,
    subtitle      => 'click on an organism name to see more details ',
    empty_message => 'Tree not available',
    collapsible   => 1,
    collapsed     => 0,
    &>

    <form action="/tools/tree_browser/" method="post">
       <input type="hidden" name="tree_string" value="<% $tree_string |h %>" />
       <input type="submit" value="show in treebrowser" />
    </form>
    <% $image_html %>
 </&>

% }
